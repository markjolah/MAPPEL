@PACKAGE_INIT@

# MappelConfig.cmake
# To enable @PROJECT_NAME@ Matlab support via MexIFace
#   use: find_package(@PROJECT_NAME@ REQUIRED COMPONENTS MATLAB)
#   These variables are set:
#       @PROJECT_NAME@_MATLAB_CODE_DIR - Matlab code directory
#       @PROJECT_NAME@_MATLAB_STARTUP_M - Matlab startup@PROJECT_NAME@.m
#
# Result:
# The following variables are set to imported interface target names under namespace '@PROJECT_NAME@::'
#   @PROJECT_NAME@_LIBRARIES  - set to shared target name if available, static otherwise
#   @PROJECT_NAME@_SHARED_LIBRARIES - shared target name
#   @PROJECT_NAME@_STATIC_LIBRARIES - static target name
#   @PROJECT_NAME@_BUILD_TYPES - List of @PROJECT_NAME@ CMAKE_BUILD_TYPE's that
#                                are built and installed installed in this prefix.

### Find dependencies
include(CMakeFindDependencyMacro)
set_and_check(PACKAGE_FIND_MODULES_PATH @PACKAGE_FIND_MODULES_PATH@)
list(INSERT CMAKE_MODULE_PATH 0 ${PACKAGE_FIND_MODULES_PATH})
#Use find_package because find_dependency does not respect COMPONENTS argument or allow
#re-finding of packages with different COMPONENTS settings
find_package(BLAS REQUIRED COMPONENTS @ARMADILLO_INT64_COMPONENT@)
find_package(LAPACK REQUIRED COMPONENTS @ARMADILLO_INT64_COMPONENT@)
find_package(Armadillo REQUIRED COMPONENTS BLAS LAPACK CXX11 @ARMADILLO_INT64_COMPONENT@)
list(REMOVE_AT CMAKE_MODULE_PATH 0) #Back to Default CMAKE Find Modules

find_dependency(BacktraceException)
find_dependency(ParallelRngManager)
find_dependency(PriorHessian)

#To enable MexIFace Matlab module use find_package(... REQUIRED COMPONENTS MATLAB)
if(@OPT_MATLAB@ AND MATLAB IN_LIST ${${PROJECT_NAME}_FIND_COMPONENTS})
    set_and_check(_MEXIFACE_CONFIG_FILE "${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@Config-mexiface.cmake")
    include(${_MEXIFACE_CONFIG_FILE})
    unset(${_MEXIFACE_CONFIG_FILE})
endif()

#Include targets file.  This will create IMPORTED target @PROJECT_NAME@
include("${CMAKE_CURRENT_LIST_DIR}/${CMAKE_SYSTEM_NAME}/@EXPORT_TARGETS_NAME@.cmake")
#Read in variable @PROJECT_NAME@_PROVIDED_COMPONENTS and @PROJECT_NAME@_<COMPONENT_NAME>_FOUND from configured file,
include("${CMAKE_CURRENT_LIST_DIR}/${CMAKE_SYSTEM_NAME}/@PROJECT_NAME@ConfigProvidedComponents.cmake")  #import @PROJECT_NAME@_PROVIDED_COMPONENTS

set(@PROJECT_NAME@_LIBRARIES @PROJECT_NAME@::@PROJECT_NAME@)
set(@PROJECT_NAME@_SHARED_LIBRARIES)
set(@PROJECT_NAME@_STATIC_LIBRARIES)
if(@BUILD_SHARED_LIBS@)
    set(@PROJECT_NAME@_SHARED_LIBRARIES @PROJECT_NAME@::@PROJECT_NAME@)
    if(@BUILD_STATIC_LIBS@)
        set(@PROJECT_NAME@_STATIC_LIBRARIES @PROJECT_NAME@::@PROJECT_NAME@_STATIC)
    endif()
else()
    set(@PROJECT_NAME@_STATIC_LIBRARIES @PROJECT_NAME@::@PROJECT_NAME@)
endif()

get_target_property(@PROJECT_NAME@_BUILD_TYPES @PROJECT_NAME@::@PROJECT_NAME@ IMPORTED_CONFIGURATIONS)

check_required_components("@PROJECT_NAME@")
